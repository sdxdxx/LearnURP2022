Shader "URP/ShaderURP_Water_SSR_PBR"
{
    Properties
    {
    	[Header(Light)]
        _Smoothness("Smoothness",range(0,1)) = 0.5
        _BlinkIntensity("Blink Intensity",float) = 1
        _BlinkThreshold("Blink Threshold",float) = 1
    	
    	[Header(Water Color)]
        _ShallowColor("Shallow Color",color) = (1.0,1.0,1.0,1.0)
        _DeepColor("Deep Color",color) = (1.0,1.0,1.0,1.0)
    	_DepthDensity("Depth Density",Range(0.1,5)) = 1
    	_ColorGradientRange("Color Gradient Range",Range(1,10)) = 1
        
    	[Header(Interactive)]
    	_RippleInt("Ripple Intensity(Vertex)",Range(0,1)) = 0.1
        
        [Header(Water Normal)]
        _NormalMap("Water Normal Map",2D) = "bump"{}
    	_NormalSpeed("Normal Speed",Vector) = (0.2,0.2,-0.33,-0.4)
        _NormalScale1("Normal Scale 1",float) = 10
        _NormalScale2("Normal Scale 2",float) = 7
        _NormalIntensity("Normal Intensity",Range(0,1)) = 0.5
        _NormalNoise("Normal Noise",Range(0,1)) = 0.68
	    
        [Header(Causitics Tex)]
        _CausiticsTex("Causitics Tex",2D) = "black"{}
        _CausiticsScale("Causitics Scale",float) = 5.7
        _CausiticsRange("Causitics Range",float) = 2.15
         _CausiticsIntensity("Causitics Intensity",float) = 1.54
        _CausiticsSpeed("Causitics Speed",float) = 1
        
        [Header(Shore)]
        _ShoreCol("Shore Col",color) = (0,0,0,0)
        _ShoreRange("Shore Range",float) = 0.08
        _ShoreEdgeWidth("Shore Edge Width",range(-1,1)) = 0.02
        _ShoreEdgeIntensity("Shore Edge Intensity",range(0,1)) = 0.2
    	
    	[Header(Foam)]
    	_FoamNoise("Foam Noise",2D) = "white"{}
        _FoamRange("Foam Range",float) = 0.1
    	_FoamBend("Foam Bend",float) = 0.2
    	_FoamFrequency("Foam Frequency",float) = 1
    	_FoamSpeed("Foam Speed", float) = 1
    	_FoamDissolve("Foam Dissolve",Range(0,2)) = 0.2
    	_FoamCol("Foam Color",color) = (1,1,1,1)
    	
	    
    	[Header(Tess)]
    	_Tess("Tessellation", Range(1, 32)) = 20
    	_MaxTessDistance("Max Tess Distance", Range(1, 32)) = 20
        _MinTessDistance("Min Tess Distance", Range(1, 32)) = 1
    	
    	[Header(Wave)]
    	_WaveA ("Wave A (dir, steepness, wavelength)", Vector) = (0.2,0,0.1,2)
    	_WaveB ("Wave B", Vector) = (0,0.2,0.05,2)
		_WaveC ("Wave C", Vector) = (0.2,0.2,0.1,2)
        _WaveInt("Wave Intensity",Range(0,1)) = 1
    }
    
    SubShader
    {
        Tags
        {
            "RenderType"="Opaque"
            "Queue"="Transparent"
            "RenderPipeline" = "UniversalPipeline"
        }
        
    	HLSLINCLUDE
    	
    		#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"
            #include "Packages/com.unity.render-pipelines.core/ShaderLibrary/SpaceTransforms.hlsl"

    		#pragma require tessellation
    		#pragma multi_compile  _MAIN_LIGHT_SHADOWS
    		#pragma multi_compile  _MAIN_LIGHT_SHADOWS_CASCADE
    		#pragma multi_compile  _SHADOWS_SOFT

    		#pragma prefer_hlslcc gles
            #pragma exclude_renderers d3d11_9x
            #pragma target 4.6

            #define kDielectricSpec half4(0.04, 0.04, 0.04, 1.0 - 0.04)
            
            //----------è´´å›¾å£°æ˜å¼€å§‹-----------
            TEXTURE2D(_CameraDepthTexture);
            TEXTURE2D(_CameraNormalsTexture);
            SAMPLER(sampler_CameraNormalsTexture);
            TEXTURE2D(_CameraOpaqueTexture);//è·å–åˆ°æ‘„åƒæœºæ¸²æŸ“ç”»é¢çš„Texture
            SAMPLER(sampler_CameraOpaqueTexture);
            
            TEXTURE2D(_ScreenSpaceReflectionTexture);//å®šä¹‰è´´å›¾
            SAMPLER(sampler_ScreenSpaceReflectionTexture);//å®šä¹‰é‡‡æ ·å™¨
            
            
            TEXTURE2D(_NormalMap);
            SAMPLER(sampler_NormalMap);
    		TEXTURE2D(_WaterRipple);
            SAMPLER(sampler_WaterRipple);
            TEXTURE2D(_CausiticsTex);
            SAMPLER(sampler_CausiticsTex);
    	
            TEXTURE2D(_FoamNoise);
            SAMPLER(sampler_FoamNoise);
    	
            //----------è´´å›¾å£°æ˜ç»“æŸ-----------
            
            CBUFFER_START(UnityPerMaterial)
            //----------å˜é‡å£°æ˜å¼€å§‹-----------
    		
            half4 _ShallowColor;
            half4 _DeepColor;
    		float _DepthDensity;
    		float _ColorGradientRange;
            
            float _NormalIntensity;
            float _NormalScale1;
            float _NormalScale2;
            float _NormalNoise;
            float4 _NormalSpeed;

    		float _RippleInt;
            
            float _Blur;
            float _CausiticsScale;
            float _CausiticsRange;
            float _CausiticsIntensity;
            float _CausiticsSpeed;
    	
            half4 _ShoreCol;
            float _ShoreRange;
            float _ShoreEdgeWidth;
            float _ShoreEdgeIntensity;
    		
            float _FoamRange;
            float _FoamFrequency;
            float _FoamSpeed;
            float _FoamBend;
            float _FoamDissolve;
            half4 _FoamCol;
            float4 _FoamNoise_ST;
    		
            float _Smoothness;
            float _BlinkIntensity;
            float _BlinkThreshold;

    		float _Tess;
    		float _MaxTessDistance;
            float _MinTessDistance;
    		
            float4 _WaveA;
            float4 _WaveB;
            float4 _WaveC;
            float _WaveInt;
            //----------å˜é‡å£°æ˜ç»“æŸ-----------
            CBUFFER_END

             //Wave: https://catlikecoding.com/unity/tutorials/flow/waves/
            float3 GerstnerWave (float4 wave, float3 p, inout float3 tangent, inout float3 binormal)
            {
			    float steepness = wave.z;
			    float wavelength = wave.w;
			    float k = 2 * PI / wavelength;
				float c = sqrt(9.8 / k);
				float2 d = normalize(wave.xy);
				float f = k * (dot(d, p.xz) - c * _Time.y);
				float a = steepness / k;
				
				p.x += d.x * (a * cos(f));
				p.y = a * sin(f);
				p.z += d.y * (a * cos(f));

				tangent += float3(
					-d.x * d.x * (steepness * sin(f)),
					d.x * (steepness * cos(f)),
					-d.x * d.y * (steepness * sin(f))
				);
				binormal += float3(
					-d.x * d.y * (steepness * sin(f)),
					d.y * (steepness * cos(f)),
					-d.y * d.y * (steepness * sin(f))
				);
				return float3(
					d.x * (a * cos(f)),
					a * sin(f),
					d.y * (a * cos(f))
				);
			}
    		
    		float3 fresnelSchlickRoughness(float cosTheta, float3 F0, float roughness)
            {
				return F0 + (max(float3(1 ,1, 1) * (1 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
			}
    	
			float3 FresnelLerp (half3 F0, half3 F90, half cosA)
			{
			    half t = Pow4 (1 - cosA);   // FAST WAY
			    return lerp (F0, F90, t);
			}

			half3 CalculateWaterBxDF(
			    float3 nDir, 
			    float3 lDir, 
			    float3 vDir, 
			    half3 waterColor,    // æ°´ä½“å›ºæœ‰è‰² (ç°åœ¨å®ƒä¼šå—å…‰ç…§å½±å“å˜æ˜æš—)
			    half3 lightCol, 
			    float smoothness, 
			    half3 refractionCol, // èƒŒæ™¯è‰² (Background)
			    half3 refCol,        // è¡¨é¢åå°„ (Skybox)
			    float shadow,
			    float transmission   // 1=æ¸…æ¾ˆ, 0=æµ‘æµŠ
			)
			{
            	float shadowRamp = 1;
			    float3 hDir = normalize(vDir + lDir);
			    float nDotl = max(saturate(dot(nDir, lDir)), 0.000001); // 0~1 çš„å…‰ç…§å¼ºåº¦
			    float nDotv = max(saturate(dot(nDir, vDir)), 0.000001);
			    
			    // --- 1. ç²—ç³™åº¦ä¸è²æ¶…å°” ---
			    float perceptualRoughness = 1 - smoothness;
			    float roughness = perceptualRoughness * perceptualRoughness;
			    float lerpSquareRoughness = pow(lerp(0.002, 1, roughness), 2);

			    float F0_val = 0.02; 
			    float fresnelTerm = F0_val + (1.0 - F0_val) * pow(1.0 - nDotv, 5.0); 
			    
			    // --- 2. æ¼«åå°„ (Diffuse) - å¡é€šæµ·æ´‹çš„æ ¸å¿ƒ ---
			    // ğŸ’¡ æŠ€å·§ï¼šå¯¹äºå¡é€šæ°´ï¼Œå¯ä»¥ä½¿ç”¨ Half-Lambert é˜²æ­¢èƒŒå…‰é¢æ­»é»‘
			    // float halfLambert = nDotl * 0.5 + 0.5; 
			    // float3 diffuseTerm = lightCol * halfLambert * shadow;
			    
			    // æ ‡å‡† Lambert:
			    // Kd: èƒ½é‡å®ˆæ’ç³»æ•°ã€‚åœ¨PBRä¸­ï¼Œåå°„è¶Šå¼ºï¼ŒæŠ˜å°„(æ¼«åå°„)è¶Šå¼±
			    // å¦‚æœæƒ³è¦æ›´"å¡é€š/å¡‘æ–™"çš„æ„Ÿè§‰ï¼Œå¯ä»¥å»æ‰ (1-fresnelTerm)
			    float3 kd = (1 - fresnelTerm) * (1.0 - 0.0); // assuming metallic is 0
			    float3 diffuseTerm = kd * lightCol * (nDotl*0.5+0.5) * shadowRamp; // è¿™é‡Œæ²¡æœ‰ä¹˜PIï¼Œé˜²æ­¢è¿‡æ›

			    // --- 3. ç…§äº®æ°´ä½“ (Lit Water Volume) ---
			    // å…³é”®é€»è¾‘ï¼šæ¼«åå°„æ˜¯ç”¨æ¥ç…§äº®"æµ‘æµŠæ°´ä½“"çš„ï¼Œè€Œä¸æ˜¯ç…§äº®"æ°´åº•çŸ³å¤´"çš„
			    // åŠ ä¸Šç¯å¢ƒå…‰(è¿™é‡Œç®€å•æ¨¡æ‹Ÿä¸º0.1çš„äº®åº¦ï¼Œä½ å¯ä»¥ä¼ å…¥ä¸“é—¨çš„Ambient)
			    float3 ambient = lightCol * 0.1; 
			    float3 litWaterBody = waterColor * (diffuseTerm + ambient);

			    // --- 4. é«˜å…‰ (Specular) ---
			    float D_denom = (pow(dot(nDir, hDir), 2) * (lerpSquareRoughness - 1) + 1);
			    float D = lerpSquareRoughness / (D_denom * D_denom * PI);
			    
			    float k = pow(roughness + 1, 2) / 8.0;
			    float G = (nDotl / (nDotl * (1-k) + k)) * (nDotv / (nDotv * (1-k) + k));
			    
			    // Specular Term
			    float3 directSpecular = (D * G * fresnelTerm) / (4 * nDotv * nDotl + 0.0001);
			    
			    // âš ï¸ ä½ ä¹‹å‰ä¿ç•™äº† PIï¼Œæˆ‘æŠŠå®ƒåŠ å›æ¥äº†ï¼Œå¦‚æœä½ è§‰å¾—é«˜å…‰å¤ªçˆ†å¯ä»¥å»æ‰
			    float3 specularResult = directSpecular * lightCol * nDotl * PI * shadowRamp;

			    // --- 5. æ°´ä¸‹æ··åˆ (Under Water Mix) ---
			    // é€»è¾‘ï¼š
			    // å¦‚æœæ°´æ¸…æ¾ˆ (transmission=1) -> æ˜¾ç¤º refractionCol (ä¸å—æ°´é¢æ³•çº¿æ¼«åå°„å½±å“)
			    // å¦‚æœæ°´æµ‘æµŠ (transmission=0) -> æ˜¾ç¤º litWaterBody (å—æ¼«åå°„å½±å“ï¼Œæœ‰æ³¢æµªç«‹ä½“æ„Ÿ)
			    half3 underWaterColor = lerp(litWaterBody, refractionCol, transmission); 
			    
			    // --- 6. æœ€ç»ˆè²æ¶…å°”æ··åˆ ---
			    half3 finalColor = lerp(underWaterColor, refCol, fresnelTerm);
			    
			    // --- 7. å åŠ é«˜å…‰ ---
			    finalColor += specularResult;

			    return finalColor;
			}

            //Remap
            float remap(float original_value, float original_min, float original_max, float new_min, float new_max)
             {
               return new_min + (((original_value - original_min) / (original_max - original_min)) * (new_max - new_min));
             }

            float3 UnpackScaleNormal(float4 packedNormal, float bumpScale)
            {
	            float3 normal = UnpackNormal(packedNormal);
            	normal.xy *= bumpScale;
            	normal.z = sqrt(1.0 - saturate(dot(normal.xy, normal.xy)));
            	return normal;
            }

            float3 NormalBlendReoriented(float3 A, float3 B)
			{
				float3 t = A.xyz + float3(0.0, 0.0, 1.0);
				float3 u = B.xyz * float3(-1.0, -1.0, 1.0);
				return (t / t.z) * dot(t, u) - u;
			}

            float3 ReconstructWorldPositionFromDepth(float4 screenPos, float rawDepth)
            {
                float2 ndcPos = (screenPos/screenPos.w)*2-1;//map[0,1] -> [-1,1]
            	float3 worldPos;
                if (unity_OrthoParams.w)
                {
					float depth01 = 1-rawDepth;
                	float3 viewPos = float3(unity_OrthoParams.xy * ndcPos.xy, 0);
                	viewPos.z = -lerp(_ProjectionParams.y, _ProjectionParams.z, depth01);
                	worldPos = mul(UNITY_MATRIX_I_V, float4(viewPos, 1)).xyz;
                }
                else
                {
	                float depth01 = Linear01Depth(rawDepth,_ZBufferParams);
                	float3 clipPos = float3(ndcPos.x,ndcPos.y,1)*_ProjectionParams.z;// z = far plane = mvp result w
	                float3 viewPos = mul(unity_CameraInvProjection,clipPos.xyzz).xyz * depth01;
	                worldPos = mul(UNITY_MATRIX_I_V,float4(viewPos,1)).xyz;
                }
            	
                return worldPos;
            }
    	
            struct vertexInput
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
            	float4 tangent : TANGENT;
            	float4 color : COLOR;
                float2 uv : TEXCOORD0;
            	
            };

            struct vertexOutput
            {
                float4 pos : SV_POSITION;
                float2 uv : TEXCOORD0;
                float4 screenPos : TEXCOORD1;
                float3 posWS : TEXCOORD2;
                float3 nDirWS : TEXCOORD3;
                float3 tDirWS : TEXCOORD4;
                float3 bDirWS : TEXCOORD5;
            };

    		 // ä¸ºäº†ç¡®å®šå¦‚ä½•ç»†åˆ†ä¸‰è§’å½¢ï¼ŒGPUä½¿ç”¨äº†å››ä¸ªç»†åˆ†å› å­ã€‚ä¸‰è§’å½¢é¢ç‰‡çš„æ¯ä¸ªè¾¹ç¼˜éƒ½æœ‰ä¸€ä¸ªå› æ•°ã€‚
            // ä¸‰è§’å½¢çš„å†…éƒ¨ä¹Ÿæœ‰ä¸€ä¸ªå› ç´ ã€‚ä¸‰ä¸ªè¾¹ç¼˜å‘é‡å¿…é¡»ä½œä¸ºå…·æœ‰SV_TessFactorè¯­ä¹‰çš„floatæ•°ç»„ä¼ é€’ã€‚
            // å†…éƒ¨å› ç´ ä½¿ç”¨SV_InsideTessFactorè¯­ä¹‰
            struct TessellationFactors
            {
                float edge[3] : SV_TessFactor;
                float inside : SV_InsideTessFactor;
            };

            // è¯¥ç»“æ„çš„å…¶ä½™éƒ¨åˆ†ä¸Attributesç›¸åŒï¼Œåªæ˜¯ä½¿ç”¨INTERNALTESSPOSä»£æ›¿POSITIONè¯­æ„ï¼Œå¦åˆ™ç¼–è¯‘å™¨ä¼šæŠ¥ä½ç½®è¯­ä¹‰çš„é‡ç”¨
            struct ControlPoint
            {
                float4 vertex : INTERNALTESSPOS;
            	float3 normal : NORMAL;
            	float4 tangent : TANGENT;
                float2 uv : TEXCOORD0;
                float4 color : COLOR;
                
            };

    		ControlPoint vert(vertexInput v)
    		{
    			ControlPoint p;

    			p.vertex = v.vertex;
    			p.uv = v.uv;
    			p.normal = v.normal;
    			p.tangent = v.tangent;
    			p.color = v.color;

    			return p;
    		}

    		 // éšç€è·ç›¸æœºçš„è·ç¦»å‡å°‘ç»†åˆ†æ•°
            float CalcDistanceTessFactor(float4 vertex, float minDist, float maxDist, float tess)
            {
                float3 worldPosition = TransformObjectToWorld(vertex.xyz);
                float dist = distance(worldPosition,  GetCameraPositionWS());
                float f = clamp(1.0 - (dist - minDist) / (maxDist - minDist), 0.01, 1.0) * tess;
                return (f);
            }

    		// Patch Constant Functionå†³å®šPatchçš„å±æ€§æ˜¯å¦‚ä½•ç»†åˆ†çš„ã€‚è¿™æ„å‘³ç€å®ƒæ¯ä¸ªPatchä»…è¢«è°ƒç”¨ä¸€æ¬¡ï¼Œ
            // è€Œä¸æ˜¯æ¯ä¸ªæ§åˆ¶ç‚¹è¢«è°ƒç”¨ä¸€æ¬¡ã€‚è¿™å°±æ˜¯ä¸ºä»€ä¹ˆå®ƒè¢«ç§°ä¸ºå¸¸é‡å‡½æ•°ï¼Œåœ¨æ•´ä¸ªPatchä¸­éƒ½æ˜¯å¸¸é‡çš„åŸå› ã€‚
            // å®é™…ä¸Šï¼Œæ­¤åŠŸèƒ½æ˜¯ä¸HullProgramå¹¶è¡Œè¿è¡Œçš„å­é˜¶æ®µã€‚
            // ä¸‰è§’å½¢é¢ç‰‡çš„ç»†åˆ†æ–¹å¼ç”±å…¶ç»†åˆ†å› å­æ§åˆ¶ã€‚æˆ‘ä»¬åœ¨MyPatchConstantFunctionä¸­ç¡®å®šè¿™äº›å› ç´ ã€‚
            // å½“å‰ï¼Œæˆ‘ä»¬æ ¹æ®å…¶è·ç¦»ç›¸æœºçš„ä½ç½®æ¥è®¾ç½®ç»†åˆ†å› å­
            TessellationFactors MyPatchConstantFunction(InputPatch<ControlPoint, 3> patch)
            {
                float minDist = _MinTessDistance;
                float maxDist = _MaxTessDistance;
            
                TessellationFactors f;
            
                float edge0 = CalcDistanceTessFactor(patch[0].vertex, minDist, maxDist, _Tess);
                float edge1 = CalcDistanceTessFactor(patch[1].vertex, minDist, maxDist, _Tess);
                float edge2 = CalcDistanceTessFactor(patch[2].vertex, minDist, maxDist, _Tess);
            
                // make sure there are no gaps between different tessellated distances, by averaging the edges out.
                f.edge[0] = (edge1 + edge2) / 2;
                f.edge[1] = (edge2 + edge0) / 2;
                f.edge[2] = (edge0 + edge1) / 2;
                f.inside = (edge0 + edge1 + edge2) / 3;
                return f;
            }

            //ç»†åˆ†é˜¶æ®µéå¸¸çµæ´»ï¼Œå¯ä»¥å¤„ç†ä¸‰è§’å½¢ï¼Œå››è¾¹å½¢æˆ–ç­‰å€¼çº¿ã€‚æˆ‘ä»¬å¿…é¡»å‘Šè¯‰å®ƒå¿…é¡»ä½¿ç”¨ä»€ä¹ˆè¡¨é¢å¹¶æä¾›å¿…è¦çš„æ•°æ®ã€‚
            //è¿™æ˜¯ hull ç¨‹åºçš„å·¥ä½œã€‚Hull ç¨‹åºåœ¨æ›²é¢è¡¥ä¸ä¸Šè¿è¡Œï¼Œè¯¥æ›²é¢è¡¥ä¸ä½œä¸ºå‚æ•°ä¼ é€’ç»™å®ƒã€‚
            //æˆ‘ä»¬å¿…é¡»æ·»åŠ ä¸€ä¸ªInputPatchå‚æ•°æ‰èƒ½å®ç°è¿™ä¸€ç‚¹ã€‚Patchæ˜¯ç½‘æ ¼é¡¶ç‚¹çš„é›†åˆã€‚å¿…é¡»æŒ‡å®šé¡¶ç‚¹çš„æ•°æ®æ ¼å¼ã€‚
            //ç°åœ¨ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ControlPointç»“æ„ã€‚åœ¨å¤„ç†ä¸‰è§’å½¢æ—¶ï¼Œæ¯ä¸ªè¡¥ä¸å°†åŒ…å«ä¸‰ä¸ªé¡¶ç‚¹ã€‚æ­¤æ•°é‡å¿…é¡»æŒ‡å®šä¸ºInputPatchçš„ç¬¬äºŒä¸ªæ¨¡æ¿å‚æ•°
            //Hullç¨‹åºçš„å·¥ä½œæ˜¯å°†æ‰€éœ€çš„é¡¶ç‚¹æ•°æ®ä¼ é€’åˆ°ç»†åˆ†é˜¶æ®µã€‚å°½ç®¡å‘å…¶æä¾›äº†æ•´ä¸ªè¡¥ä¸ï¼Œ
            //ä½†è¯¥å‡½æ•°ä¸€æ¬¡ä»…åº”è¾“å‡ºä¸€ä¸ªé¡¶ç‚¹ã€‚è¡¥ä¸ä¸­çš„æ¯ä¸ªé¡¶ç‚¹éƒ½ä¼šè°ƒç”¨ä¸€æ¬¡å®ƒï¼Œå¹¶å¸¦æœ‰ä¸€ä¸ªé™„åŠ å‚æ•°ï¼Œ
            //è¯¥å‚æ•°æŒ‡å®šåº”è¯¥ä½¿ç”¨å“ªä¸ªæ§åˆ¶ç‚¹ï¼ˆé¡¶ç‚¹ï¼‰ã€‚è¯¥å‚æ•°æ˜¯å…·æœ‰SV_OutputControlPointIDè¯­ä¹‰çš„æ— ç¬¦å·æ•´æ•°ã€‚
            [domain("tri")]//æ˜ç¡®åœ°å‘Šè¯‰ç¼–è¯‘å™¨æ­£åœ¨å¤„ç†ä¸‰è§’å½¢ï¼Œå…¶ä»–é€‰é¡¹ï¼š
            [outputcontrolpoints(3)]//æ˜ç¡®åœ°å‘Šè¯‰ç¼–è¯‘å™¨æ¯ä¸ªè¡¥ä¸è¾“å‡ºä¸‰ä¸ªæ§åˆ¶ç‚¹
            [outputtopology("triangle_cw")]//å½“GPUåˆ›å»ºæ–°ä¸‰è§’å½¢æ—¶ï¼Œå®ƒéœ€è¦çŸ¥é“æˆ‘ä»¬æ˜¯å¦è¦æŒ‰é¡ºæ—¶é’ˆæˆ–é€†æ—¶é’ˆå®šä¹‰å®ƒä»¬
            [partitioning("fractional_odd")]//å‘ŠçŸ¥GPUåº”è¯¥å¦‚ä½•åˆ†å‰²è¡¥ä¸ï¼Œç°åœ¨ï¼Œä»…ä½¿ç”¨æ•´æ•°æ¨¡å¼
            [patchconstantfunc("MyPatchConstantFunction")]//GPUè¿˜å¿…é¡»çŸ¥é“åº”å°†è¡¥ä¸åˆ‡æˆå¤šå°‘éƒ¨åˆ†ã€‚è¿™ä¸æ˜¯ä¸€ä¸ªæ’å®šå€¼ï¼Œæ¯ä¸ªè¡¥ä¸å¯èƒ½æœ‰æ‰€ä¸åŒã€‚å¿…é¡»æä¾›ä¸€ä¸ªè¯„ä¼°æ­¤å€¼çš„å‡½æ•°ï¼Œç§°ä¸ºè¡¥ä¸å¸¸æ•°å‡½æ•°ï¼ˆPatch Constant Functionsï¼‰
            ControlPoint hullProgram(InputPatch<ControlPoint, 3> patch, uint id : SV_OutputControlPointID)
            {
                return patch[id];
            }

			vertexOutput AfterTessVertProgram (vertexInput v)
			{
				 vertexOutput o;
            	o.posWS = TransformObjectToWorld(v.vertex);

            	//Wave
            	float3 p = TransformObjectToWorld(v.vertex);
            	float3 tangent = v.tangent;
            	float3 binormal = normalize(cross(v.normal,v.tangent)*v.tangent.w);
            	p += GerstnerWave(_WaveA, p, tangent, binormal)*_WaveInt;
			    p += GerstnerWave(_WaveB, p, tangent, binormal)*_WaveInt;
			    p += GerstnerWave(_WaveC, p, tangent, binormal)*_WaveInt;
            	float3 normal = cross(binormal, tangent);

            	v.vertex.xyz = TransformWorldToObject(p);
            	float4 posCS = TransformObjectToHClip(v.vertex.xyz);
            	o.screenPos = ComputeScreenPos(posCS);
            	
            	//Interactive Water
            	float rippleHeight  = SAMPLE_TEXTURE2D_LOD(_WaterRipple,sampler_PointClamp,o.screenPos.xy/o.screenPos.w,0).x;
            	p.y+=rippleHeight*_RippleInt*0.1f;

            	v.vertex.xyz = TransformWorldToObject(p);
            	
                posCS = TransformObjectToHClip(v.vertex.xyz);
                o.pos = posCS;
                o.nDirWS = TransformObjectToWorldNormal(v.normal);
            	o.tDirWS = normalize(TransformObjectToWorld(v.tangent));
            	o.bDirWS = normalize(cross(o.nDirWS,o.tDirWS)*v.tangent.w);
                o.uv = v.uv;
                o.screenPos = ComputeScreenPos(posCS);
                return o;
			}

            //HUllç€è‰²å™¨åªæ˜¯ä½¿æ›²é¢ç»†åˆ†å·¥ä½œæ‰€éœ€çš„ä¸€éƒ¨åˆ†ã€‚ä¸€æ—¦ç»†åˆ†é˜¶æ®µç¡®å®šäº†åº”å¦‚ä½•ç»†åˆ†è¡¥ä¸ï¼Œ
            //åˆ™ç”±Domainç€è‰²å™¨æ¥è¯„ä¼°ç»“æœå¹¶ç”Ÿæˆæœ€ç»ˆä¸‰è§’å½¢çš„é¡¶ç‚¹ã€‚
            //Domainç¨‹åºå°†è·å¾—ä½¿ç”¨çš„ç»†åˆ†å› å­ä»¥åŠåŸå§‹è¡¥ä¸çš„ä¿¡æ¯ï¼ŒåŸå§‹è¡¥ä¸åœ¨è¿™ç§æƒ…å†µä¸‹ä¸ºOutputPatchç±»å‹ã€‚
            //ç»†åˆ†é˜¶æ®µç¡®å®šè¡¥ä¸çš„ç»†åˆ†æ–¹å¼æ—¶ï¼Œä¸ä¼šäº§ç”Ÿä»»ä½•æ–°çš„é¡¶ç‚¹ã€‚ç›¸åï¼Œå®ƒä¼šä¸ºè¿™äº›é¡¶ç‚¹æä¾›é‡å¿ƒåæ ‡ã€‚
            //ä½¿ç”¨è¿™äº›åæ ‡æ¥å¯¼å‡ºæœ€ç»ˆé¡¶ç‚¹å–å†³äºåŸŸç€è‰²å™¨ã€‚ä¸ºäº†ä½¿ä¹‹æˆä¸ºå¯èƒ½ï¼Œæ¯ä¸ªé¡¶ç‚¹éƒ½ä¼šè°ƒç”¨ä¸€æ¬¡åŸŸå‡½æ•°ï¼Œå¹¶ä¸ºå…¶æä¾›é‡å¿ƒåæ ‡ã€‚
            //å®ƒä»¬å…·æœ‰SV_DomainLocationè¯­ä¹‰ã€‚
            //åœ¨Demainå‡½æ•°é‡Œé¢ï¼Œæˆ‘ä»¬å¿…é¡»ç”Ÿæˆæœ€ç»ˆçš„é¡¶ç‚¹æ•°æ®ã€‚
            [domain("tri")]//Hullç€è‰²å™¨å’ŒDomainç€è‰²å™¨éƒ½ä½œç”¨äºç›¸åŒçš„åŸŸï¼Œå³ä¸‰è§’å½¢ã€‚æˆ‘ä»¬é€šè¿‡domainå±æ€§å†æ¬¡å‘å‡ºä¿¡å·
            vertexOutput domainProgram(TessellationFactors factors, OutputPatch<ControlPoint, 3> patch, float3 barycentricCoordinates : SV_DomainLocation)
            {
                vertexInput v;
        
                //ä¸ºäº†æ‰¾åˆ°è¯¥é¡¶ç‚¹çš„ä½ç½®ï¼Œæˆ‘ä»¬å¿…é¡»ä½¿ç”¨é‡å¿ƒåæ ‡åœ¨åŸå§‹ä¸‰è§’å½¢èŒƒå›´å†…è¿›è¡Œæ’å€¼ã€‚
                //Xï¼ŒYå’ŒZåæ ‡ç¡®å®šç¬¬ä¸€ï¼Œç¬¬äºŒå’Œç¬¬ä¸‰æ§åˆ¶ç‚¹çš„æƒé‡ã€‚
                //ä»¥ç›¸åŒçš„æ–¹å¼æ’å€¼æ‰€æœ‰é¡¶ç‚¹æ•°æ®ã€‚è®©æˆ‘ä»¬ä¸ºæ­¤å®šä¹‰ä¸€ä¸ªæ–¹ä¾¿çš„å®ï¼Œè¯¥å®å¯ç”¨äºæ‰€æœ‰çŸ¢é‡å¤§å°ã€‚
                #define DomainInterpolate(fieldName) v.fieldName = \
                        patch[0].fieldName * barycentricCoordinates.x + \
                        patch[1].fieldName * barycentricCoordinates.y + \
                        patch[2].fieldName * barycentricCoordinates.z;
    
                    //å¯¹ä½ç½®ã€é¢œè‰²ã€UVã€æ³•çº¿ç­‰è¿›è¡Œæ’å€¼
                    DomainInterpolate(vertex)
                    DomainInterpolate(uv)
                    DomainInterpolate(color)
                    DomainInterpolate(normal)
    				DomainInterpolate(tangent);
                    
                    //ç°åœ¨ï¼Œæˆ‘ä»¬æœ‰äº†ä¸€ä¸ªæ–°çš„é¡¶ç‚¹ï¼Œè¯¥é¡¶ç‚¹å°†åœ¨æ­¤é˜¶æ®µä¹‹åå‘é€åˆ°å‡ ä½•ç¨‹åºæˆ–æ’å€¼å™¨ã€‚
                    //ä½†æ˜¯è¿™äº›ç¨‹åºéœ€è¦Varyingsæ•°æ®ï¼Œè€Œä¸æ˜¯Attributesã€‚ä¸ºäº†è§£å†³è¿™ä¸ªé—®é¢˜ï¼Œ
                    //æˆ‘ä»¬è®©åŸŸç€è‰²å™¨æ¥ç®¡äº†åŸå§‹é¡¶ç‚¹ç¨‹åºçš„èŒè´£ã€‚
                    //è¿™æ˜¯é€šè¿‡è°ƒç”¨å…¶ä¸­çš„AfterTessVertProgramï¼ˆä¸å…¶ä»–ä»»ä½•å‡½æ•°ä¸€æ ·ï¼‰å¹¶è¿”å›å…¶ç»“æœæ¥å®Œæˆçš„ã€‚
                    return AfterTessVertProgram(v);
            }
    		
            half4 frag (vertexOutput i) : SV_TARGET
            {
				float2 screenPos = i.screenPos.xy/i.screenPos.w;
	            
	            // --- 1. Vector Setup (Moved Up for Physics Calc) ---
	            float3 vDirWS = normalize(_WorldSpaceCameraPos.xyz - i.posWS.xyz);
	            
	            float3x3 TBN = float3x3(
	              i.tDirWS.x, i.bDirWS.x, i.nDirWS.x,
	              i.tDirWS.y, i.bDirWS.y, i.nDirWS.y,
	              i.tDirWS.z, i.bDirWS.z, i.nDirWS.z
	            );
	            
	            // --- 2. Water Normal ---
	            float2 normalUV = i.posWS.xz;
	            float2 normalUV1 = normalUV/_NormalScale1 + frac(_NormalSpeed.xy*0.1*_Time.y);
	            float2 normalUV2 = normalUV/_NormalScale2 + frac(_NormalSpeed.zw*0.1*_Time.y);
	            float4 NormalMap1 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV1);
	            float4 NormalMap2 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV2);
	            float3 var_NormalMap1 = UnpackScaleNormal(NormalMap1,_NormalIntensity);
	            float3 var_NormalMap2 = UnpackScaleNormal(NormalMap2,_NormalIntensity);
	            float3 waterNormal = NormalBlendReoriented(var_NormalMap1,var_NormalMap2);
            	
	            // Interactive Ripples
	            float3 rippleNormal= SAMPLE_TEXTURE2D(_WaterRipple,sampler_WaterRipple,screenPos);
	            
	            // Blend & Transform Normal
	            waterNormal = waterNormal + rippleNormal;
	            waterNormal = mul(TBN, waterNormal);
	            waterNormal = normalize(waterNormal);
            	
	            
	            float2 noiseUV = waterNormal.xz/(1+i.pos.w);
	            
	            // --- 3. Depth & Distortion ---
	            // Get Original Depth
	            float rawDepth0 = SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,screenPos).r;
	            float3 posWS_frag0 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth0);
	            float waterDepth0 = i.posWS.y - posWS_frag0.y;
	            
	            // Firstly Sample Depth Texture (Distortion)
	            float2 grabUV = screenPos;
	            grabUV.x += noiseUV*_NormalNoise;
	            float rawDepth1 =  SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,grabUV).r;
	            float3 posWS_frag1 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth1);

	            // Get Reflection And Refraction Mask
	            float refractionMask = step(posWS_frag1.y, i.posWS.y);
	            // Apply mask to UV jitter
	            grabUV = screenPos;
	            grabUV.x += noiseUV*_NormalNoise/max(i.screenPos.w,1.2f) * refractionMask;

	            // Secondly Sample Depth Texture (The clean depth for logic)
	            float rawDepth2 =  SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,grabUV).r;
	            float3 posWS_frag2 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth2);
	            
	            // æœ€ç»ˆç”¨äºè®¡ç®—é¢œè‰²çš„ç‰©ç†æ·±åº¦ (Vertical Depth)
	            float waterDepth = i.posWS.y - posWS_frag2.y;
	            
	            // --- 4. Caustics & Background ---
	            float causitics_range = saturate(exp(-waterDepth/_CausiticsRange));
	            float2 causiticsUV = posWS_frag2.xz/_CausiticsScale;
	            float2 causiticsUV1 = causiticsUV+frac(_Time.x*_CausiticsSpeed);
	            float2 causiticsUV2 = causiticsUV-frac(_Time.x*_CausiticsSpeed);
	            half3 CausiticsCol1 = SAMPLE_TEXTURE2D(_CausiticsTex,sampler_CausiticsTex,causiticsUV1+float2(0.1f,0.2f));
	            half3 CausiticsCol2 = SAMPLE_TEXTURE2D(_CausiticsTex,sampler_CausiticsTex,causiticsUV2);
	            float3 CameraNormal = SAMPLE_TEXTURE2D(_CameraNormalsTexture,sampler_CameraNormalsTexture,grabUV);
	            float CausticsMask1 = saturate(CameraNormal.y*CameraNormal.y);
	            float CausticsMask2 = saturate(dot(CameraNormal,_MainLightPosition));
	            float CausticsMask = CausticsMask1*CausticsMask2;
	            half3 CausiticsCol = min(CausiticsCol1,CausiticsCol2)*causitics_range*_CausiticsIntensity*CausticsMask;

	            // Refraction UnderWater (Background + Caustics)
	            half3 underWaterCol = SAMPLE_TEXTURE2D(_CameraOpaqueTexture,sampler_CameraOpaqueTexture,grabUV);
	            underWaterCol = saturate(underWaterCol + CausiticsCol);
	            
	            // Reflection Color
	            float2 reflectUV = screenPos;
	            half4 refCol = SAMPLE_TEXTURE2D(_ScreenSpaceReflectionTexture,sampler_ScreenSpaceReflectionTexture,reflectUV);
            	
	            // ==========================================================
	            // --- 5. Water Physics Color (Corrected Logic) ---
	            // ==========================================================
	            
	            // A. è®¡ç®—å…‰çº¿åœ¨æ°´ä¸‹çš„å®é™…è·¯å¾„é•¿åº¦ (View Path Length)
	            // å‚ç›´æ·±åº¦ (Vertical Depth)
	            float verticalDepth = max(waterDepth, 0.0);
	            // è§†çº¿è§’åº¦ä¿®æ­£ (Slant Factor): è§’åº¦è¶Šå¹³ï¼ŒNdotVè¶Šå°ï¼Œè·¯å¾„è¶Šé•¿
	            float NdotV = max(dot(waterNormal, vDirWS), 0.001); 
	            float viewPathLength = verticalDepth / NdotV;

	            // B. è®¡ç®—é€å…‰ç‡ (Transmission) - Beer-Lambert Law
	            // _DepthDensity è¶Šå¤§ï¼Œæ°´è¶Šæ··æµŠï¼Œå¸æ”¶è¶Šå¿« (å»ºè®®èŒƒå›´ 0.1 ~ 5.0)
            	// transmission = 1 (æ¸…æ¾ˆ/æµ…), transmission = 0 (æµ‘æµŠ/æ·±)
	            float transmission = exp(-viewPathLength * _DepthDensity);

	            // C. è®¡ç®—æ°´ä½“å›ºæœ‰è‰² (Volume Color)
	            // ä»…åŸºäºå‚ç›´æ·±åº¦å†³å®šæ˜¯æµ…æ°´è‰²è¿˜æ˜¯æ·±æ°´è‰²
	            // _ColorGradientRange æ§åˆ¶é¢œè‰²è¿‡æ¸¡çš„æ·±åº¦ (å»ºè®®èŒƒå›´ 1.0 ~ 10.0)
            	float colorGradient =1-clamp(exp(-max(0,verticalDepth)/_ColorGradientRange),0,1);
	            half3 volumeColor = lerp(_ShallowColor.rgb, _DeepColor.rgb, colorGradient);
            	
	            // ==========================================================

	            // Metallic & Smoothness
	            float smoothness = _Smoothness;
	            
	            // MainLight
	            float4 shadowCoord = TransformWorldToShadowCoord(i.posWS);
	            Light mainLight = GetMainLight(shadowCoord);
	            half3 mainLightColor = mainLight.color;
	            float3 mainLightDir = mainLight.direction;
	            float mainLightShadow = MainLightRealtimeShadow(shadowCoord);
	            float3 mainLightRadiance = mainLightColor * mainLight.distanceAttenuation;
	            
	            // Final Physics Calculation
	            // æ³¨æ„ï¼šå‚æ•°å·²æ›´æ–°ï¼Œä¸å†ä¼ é€’é”™è¯¯çš„ Lerp ç»“æœï¼Œè€Œæ˜¯ä¼ é€’ç‰©ç†å‚æ•°
	            // å‚æ•°é¡ºåº: Normal, LightDir, ViewDir, Albedo(0), LightColor, Smoothness, Metallic(0), RefractionBG, Reflection, VolumeColor, Shadow, Transmission
	            half3 WaterFinalColor = CalculateWaterBxDF(
	                waterNormal, 
	                mainLightDir, 
	                vDirWS, 
	                volumeColor, 
	                mainLightRadiance,
	                smoothness, 
	                underWaterCol,
	                refCol.rgb,
	                mainLightShadow, 
	                transmission
	            );
            	
            	
            	//blink
                float3 blinkNormal1 = var_NormalMap1;
                float3 blinkNormal2 = var_NormalMap2;
                float3 blinkNormal;
                blinkNormal.xy = (blinkNormal1.xy + blinkNormal2.xy)/2*_BlinkIntensity;
                blinkNormal.z = 1-sqrt(dot(blinkNormal.xy,blinkNormal.xy));
                blinkNormal = mul(TBN,blinkNormal);
                float2 blinkUV = blinkNormal.xz/(1+i.pos.w);
                half3 blink = SAMPLE_TEXTURE2D(_ScreenSpaceReflectionTexture,sampler_ScreenSpaceReflectionTexture,screenPos+blinkUV * _NormalNoise);
                blink = max(0,blink-_BlinkThreshold);//Use _BlinkThreshold to remove unnecessary part
             
            	//ShoreEdge
            	half3 shoreCol = _ShoreCol;
                float shoreRange = saturate(exp(-max(waterDepth0,waterDepth)/_ShoreRange));
                half3 shoreEdge = smoothstep(0.1,1-(_ShoreEdgeWidth-0.2),shoreRange)*shoreCol*_ShoreEdgeIntensity;
             
            	//Foam
                float foamX = saturate(1-waterDepth/_FoamRange);
                float foamRange = 1-smoothstep(_FoamBend-0.1,1,saturate(max(waterDepth0,waterDepth)/_FoamRange));//é®ç½©
                float foamNoise = SAMPLE_TEXTURE2D(_FoamNoise,sampler_FoamNoise,i.posWS.xz*_FoamNoise_ST.xy+_FoamNoise_ST.zw);
                half4 foam = sin(_FoamFrequency*foamX-_FoamSpeed*_Time.y);
                foam = saturate(step(foamRange,foam+foamNoise-_FoamDissolve))*foamRange*_FoamCol;
            	
                half3 FinalRGB = saturate(WaterFinalColor+shoreEdge+foam);
            	FinalRGB = FinalRGB+smoothstep(0.3,0.4,rippleNormal.b)*FinalRGB,
            	FinalRGB += blink;
            	
            	half4 result = half4(FinalRGB,1.0);
            	
                return result;
            }

    // 		half4 frag_Back (vertexOutput i) : SV_TARGET
    //         {
    //         	float2 screenPos = i.screenPos.xy/i.screenPos.w;
    //         	
    //         	//Vector
    //             float3x3 TBN = float3x3(
    //               i.tDirWS.x, i.bDirWS.x, i.nDirWS.x,
    //               i.tDirWS.y, i.bDirWS.y, i.nDirWS.y,
    //               i.tDirWS.z, i.bDirWS.z, i.nDirWS.z
    //             );
    //             
    //             float3 nDirWS = i.nDirWS;
    //             
    //         	
    //         	//WaterNormal
    //             float2 normalUV = i.posWS.xz;
    //         	float2 normalUV1 = normalUV/_NormalScale1 + frac(_NormalSpeed.xy*0.1*_Time.y);
    //         	float2 normalUV2 = normalUV/_NormalScale2 + frac(_NormalSpeed.zw*0.1*_Time.y);
    //             float4 NormalMap1 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV1);
    //             float4 NormalMap2 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV2);
    //             float3 var_NormalMap1 = UnpackScaleNormal(NormalMap1,_NormalIntensity);
    //             float3 var_NormalMap2 = UnpackScaleNormal(NormalMap2,_NormalIntensity);
    //             float3 waterNormal = NormalBlendReoriented(var_NormalMap1,var_NormalMap2);
    //         	//InteractiveNormal
    //         	float3 rippleNormal= SAMPLE_TEXTURE2D(_WaterRipple,sampler_WaterRipple,screenPos);
    //         	//BlendNormal
    //         	waterNormal = waterNormal+rippleNormal;
    //         	waterNormal = mul(TBN,waterNormal);
    //         	waterNormal = normalize(waterNormal);
    //         	
    //         	
    //         	//ReflectionColor
    //              float2 noiseUV = waterNormal.xz/(1+i.pos.w);
    //         	float2 reflectUV = screenPos;
    //         	reflectUV.x += noiseUV*_NormalNoise;
    //         	half4 refCol = SAMPLE_TEXTURE2D(_ScreenSpaceReflectionTexture,sampler_ScreenSpaceReflectionTexture,reflectUV);
    //         	
    //         	//WaterDepth
    //             float rawDepth0 = SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,screenPos).r;
    //             float3 posWS_frag0 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth0);
    //             float waterDepth0 =  posWS_frag0.y- i.posWS.y;
    //         	
    //         	float2 grabUV = screenPos;
    //         	grabUV.x += noiseUV*_NormalNoise;
    //         	
    //         	//ç¬¬ä¸€æ¬¡æ¬¡é‡‡æ ·æ·±åº¦å›¾ï¼ˆæ‰°åŠ¨ï¼‰
    //         	float rawDepth1 =  SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,grabUV).r;
    //             float3 posWS_frag1 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth1);
    //         	
    //         	float refractionMask = step(i.posWS.y,posWS_frag1.y);
    //         	grabUV = screenPos;
    //         	grabUV.x += noiseUV*_NormalNoise/max(i.screenPos.w,2.0f)*refractionMask;
    //
    //         	//ç¬¬äºŒæ¬¡æ¬¡é‡‡æ ·æ·±åº¦å›¾ï¼ˆå»é™¤ä¸è¯¥æ‰°åŠ¨éƒ¨åˆ†ï¼‰
    //         	float rawDepth2 =  SAMPLE_TEXTURE2D(_CameraDepthTexture,sampler_PointClamp,grabUV).r;
    //             float3 posWS_frag2 = ReconstructWorldPositionFromDepth(i.screenPos,rawDepth2);
    //         	float waterDepth = posWS_frag2.y-i.posWS.y;
    //         	
    //         	//UnderWater
				// half3 underWaterCol = SAMPLE_TEXTURE2D(_CameraOpaqueTexture,sampler_CameraOpaqueTexture,grabUV);
    //         	
    //         	//WaterColor
    //             half4 waterCol = _ShallowColor*0.2f+_DeepColor*0.8f;
    //         	
    //         	//Light
    //         	//ä¼˜åŒ–æ¨¡æ‹Ÿé˜³å…‰æ˜ ç…§æ°´é¢æ•ˆæœ
    //             float3 nDir = -waterNormal;
    //         	float3 lDir = -_MainLightPosition.xyz;
    //         	float3 vDir = normalize(_WorldSpaceCameraPos.xyz - i.posWS);
    //             float3 hDir = SafeNormalize(lDir+vDir);
    //             float nDoth = dot(nDir,hDir);
    //         	float halfLambert = saturate(dot(nDir,lDir)*0.5+0.5);
    //         	float halfLambert_Modified = remap(halfLambert,0,1,0.5,1);
    //         	float3 SpecLight = CalculateSpecularResultColor(waterCol,nDir,lDir,vDir,_Smoothness,_Metallic, _SpecCol)*_SpecInt;
    //         	
    //         	waterCol.rgb = lerp(refCol*saturate(waterCol+0.3),waterCol,(1-_RefIntensity*0.25f))*halfLambert_Modified;
    //
    //         	float FinalA = waterCol.a;
    //         	
    //         	half3 waterFinalCol = saturate(lerp(underWaterCol*waterCol,waterCol,FinalA)+SpecLight);
    //         	
    //             //Blink
    //             float3 blinkNormal1 = var_NormalMap1;
    //             float3 blinkNormal2 = var_NormalMap2;
    //             float3 blinkNormal;
    //             blinkNormal.xy = (blinkNormal1.xy + blinkNormal2.xy)/2*_BlinkIntensity;
    //             blinkNormal.z = 1-sqrt(dot(blinkNormal.xy,blinkNormal.xy));
    //             blinkNormal = mul(TBN,blinkNormal);
    //             float2 blinkUV = blinkNormal.xz/(1+i.pos.w);
    //             half3 blink = SAMPLE_TEXTURE2D(_ScreenSpaceReflectionTexture,sampler_ScreenSpaceReflectionTexture,screenPos+blinkUV * _NormalNoise);
    //             blink = max(0,blink-_BlinkThreshold);//ä½¿ç”¨_BlinkThresholdå»é™¤ä¸è¦çš„éƒ¨åˆ†
    //
    //         	//ShoreEdge
    //         	half3 shoreCol = _ShoreCol;
    //             float shoreRange = saturate(exp(-max(waterDepth0,waterDepth)/_ShoreRange));
    //             half3 shoreEdge = smoothstep(0.1,1-(_ShoreEdgeWidth-0.2),shoreRange)*shoreCol*_ShoreEdgeIntensity;
    //         	
    //         	//Foam
    //             float foamX = saturate(1-waterDepth/_FoamRange);
    //             float foamRange = 1-smoothstep(_FoamBend-0.1,1,saturate(max(waterDepth0,waterDepth)/_FoamRange));//Mask
    //         	
    //             float foamNoise = SAMPLE_TEXTURE2D(_FoamNoise,sampler_FoamNoise,i.posWS.xz*_FoamNoise_ST.xy+_FoamNoise_ST.zw);
    //             half4 foam = sin(_FoamFrequency*foamX-_FoamSpeed*_Time.y);
    //             foam = saturate(step(foamRange,foam+foamNoise-_FoamDissolve))*foamRange*_FoamCol;
    //         	
    //             half3 FinalRGB = saturate(waterFinalCol+foam+shoreEdge);
    //         	FinalRGB = FinalRGB+smoothstep(0.3,0.4,rippleNormal.b)*FinalRGB,
    //         	FinalRGB += blink;
    //         	half4 result = half4(FinalRGB,1.0);
    //         	
    //             return result;
    //         }
    	
    	ENDHLSL
    	
    	pass
        {
	        Name "WaterMask"

        	Tags{"LightMode" = "WaterMask"}

            HLSLPROGRAM
            
            #pragma vertex vert
            #pragma hull hullProgram
            #pragma domain domainProgram
            #pragma fragment frag_UnderWaterMask

            half4 frag_UnderWaterMask (vertexOutput i) : SV_TARGET
            {
            	return half4(1,1,1,1);
            }
            
            ENDHLSL
        }


        pass
        {
	        Name "WaterFront"

        	Cull Back
        	Tags{"LightMode" = "UniversalForward"}


            HLSLPROGRAM
            
            #pragma vertex vert
            #pragma hull hullProgram
            #pragma domain domainProgram
            #pragma fragment frag
            
            ENDHLSL
        }
    	
//    	pass
//        {
//	        Name "WaterBack"
//
//        	Cull Front
//        	Tags{"LightMode" = "SRPDefaultUnlit"}
//
//            HLSLPROGRAM
//            
//            #pragma vertex vert
//            #pragma hull hullProgram
//            #pragma domain domainProgram
//            #pragma fragment frag_Back
//            
//            ENDHLSL
//        }

	    //DepthOnly
        pass
        {
        	Name "CustomDepthOnly"
            Tags
            {
                "LightMode" = "DepthOnly"
            }
	        
            ZWrite On
            ColorMask R
            Cull Off
            HLSLPROGRAM
            #pragma target 2.0

            #pragma vertex vert
            #pragma hull hullProgram
            #pragma domain domainProgram
            #pragma fragment frag_DepthOnly

            half4 frag_DepthOnly(vertexOutput i) : SV_TARGET
			{
				return i.pos.z;
			}
            
            ENDHLSL
        }

		//DepthNormals
		pass
        {
        	Name "CustomNormalsPass"

        	Tags{"LightMode" = "DepthNormals"}
        	Cull Off
            HLSLPROGRAM

            #pragma vertex vert
            #pragma hull hullProgram
            #pragma domain domainProgram
            #pragma fragment frag_DepthNormals

            half4 frag_DepthNormals(vertexOutput i) : SV_TARGET
			{
				float2 screenPos = i.screenPos.xy/i.screenPos.w;
				
	            float3x3 TBN = float3x3(
	              i.tDirWS.x, i.bDirWS.x, i.nDirWS.x,
	              i.tDirWS.y, i.bDirWS.y, i.nDirWS.y,
	              i.tDirWS.z, i.bDirWS.z, i.nDirWS.z
	            );
				
	            float2 normalUV = i.posWS.xz;
	            float2 normalUV1 = normalUV/_NormalScale1 + frac(_NormalSpeed.xy*0.1*_Time.y);
	            float2 normalUV2 = normalUV/_NormalScale2 + frac(_NormalSpeed.zw*0.1*_Time.y);
	            float4 NormalMap1 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV1);
	            float4 NormalMap2 = SAMPLE_TEXTURE2D(_NormalMap,sampler_NormalMap,normalUV2);
	            float3 var_NormalMap1 = UnpackScaleNormal(NormalMap1,_NormalIntensity*_NormalNoise);
	            float3 var_NormalMap2 = UnpackScaleNormal(NormalMap2,_NormalIntensity*_NormalNoise);
	            float3 waterNormal = NormalBlendReoriented(var_NormalMap1,var_NormalMap2);
				
	            float3 rippleNormal= SAMPLE_TEXTURE2D(_WaterRipple,sampler_WaterRipple,screenPos);
				
	            waterNormal = waterNormal + rippleNormal;
	            waterNormal = mul(TBN, waterNormal);
	            waterNormal = normalize(waterNormal);
				
				return float4(waterNormal,i.pos.z);
			}

            ENDHLSL
            
        }
    	
    	

    }
}